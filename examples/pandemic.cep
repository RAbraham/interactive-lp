% Single stage version, demonstrating player actions and
% handing off turns between players.

city : type.
player : type.
card : type.
% city cards are card-forming constants that take a city argument
city_card city : card.

adjacent city city : bwd. % Edge on the map graph

res_ctr city : pred.      % Research center
disease city : pred.      % City has disease cube
at player city : pred.    % Player is at city
turn player : pred.       % Player has a turn
hand player card : pred.  % Player has card in hand

% For handling player turns
active_player player : pred. % Player is currently active
after player player : bwd.  % Player goes after other player

% Player actions
stage play = {
  drive :
    turn P * at P C * adjacent C C'
  -o
    at P C'.

  fly :
    turn P * at P C * hand P (city_card C')
  -o
    at P C'.

  build :
    turn P * $at P C * hand P (city_card C)
  -o
    res_ctr C.

  treat :
    turn P * $at P C * disease C
  -o
    ().

  % Spend turns without doing anything
  nothing : turn P -o ().

} #interactive play.

next_player : 
  qui * stage play * active_player P
  * after P P'
-o
  stage play *
  active_player P'
  * turn P' * turn P' * turn P' * turn P'.


atlanta : city.
los_angeles : city.
bogota : city.
kinshasa : city.
delhi : city.

adjacent atlanta los_angeles.
adjacent los_angeles atlanta.
adjacent los_angeles bogota.
adjacent bogota los_angeles.
adjacent bogota kinshasa.
adjacent kinshasa delhi.
adjacent delhi kinshasa.
adjacent delhi los_angeles.
adjacent los_angeles delhi.

player_a : player.
player_b : player.
player_c : player.
player_d : player.

after player_a player_b.
after player_b player_c.
after player_c player_d.
after player_d player_a.

context init = {
  res_ctr atlanta,
  at player_a atlanta,
  at player_b atlanta,
  at player_c atlanta,
  at player_d atlanta,
  active_player player_a,
  turn player_a, turn player_a,
  turn player_a, turn player_a,
  hand player_a (city_card bogota),
  hand player_a (city_card los_angeles),
  disease delhi
}.

#trace _ play init.
